use std::str::FromStr;
use crate::ast::tigerabs::*;
use crate::ast::position::*;
use crate::ast::lexer;

grammar;

pub Expr: Box<Exp> = {
    "let" <decs:DecsEx> "in" "end" => {
        let unit = posed_exp(_Exp::UnitExp, 0, 0);

        let no_pos_exp = _Exp::LetExp {
            decs,
            body: unit,
        };

        return posed_exp(no_pos_exp, 0, 0);
    },
    "let" <decs: DecsEx> "in" <body: Expr> "end" => {
        let no_pos_exp = _Exp::LetExp {
            decs,
            body,
        };

        return posed_exp(no_pos_exp, 0, 0);
    },
    "let" <decs: DecsEx> "in" <list: ExpList> "end" => {
        let seq = _Exp::SeqExp(list);

        let no_pos_exp = _Exp::LetExp {
            decs,
            body: posed_exp(seq, 0, 0),
        };

        return posed_exp(no_pos_exp, 0, 0);
    },
    LoopExp,
}

LoopExp: Box<Exp> = {
    "for" <var:Sym> ":=" <lo:IfThenExp> "to" <hi:IfThenExp> "do" <body: Expr> => {
        let no_pos_exp = _Exp::ForExp {
            var,
            escape: false,
            lo,
            hi,
            body,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    "while" <test:LoopExp> "do" <body:Expr> => {
        let no_pos_exp = _Exp::WhileExp {
            test,
            body,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },

    AssExp,
}

AssExp: Box<Exp> = {
    <v:VarEx> ":=" <e:ArrExp> => {
        let no_pos_exp = _Exp::AssignExp {var: v, exp: e};
        return posed_exp(no_pos_exp, 0, 0);
    },
    ArrExp,
}

ArrExp: Box<Exp> = {
    // svar parses a VarEx which can only be a SimpleVar. Crazy hack brought from the yacc/bison parser
    <svar: VarEx> "[" <size: Expr> "]" "of" <init: IfThenExp> => {
        let no_pos_exp = _Exp::ArrayExp {
            typ: var_name(svar),
            size,
            init,
        };

        return posed_exp(no_pos_exp, 0, 0);
    },

    IfThenExp,
}

IfThenExp: Box<Exp> = {
    "if" <test:IfThenElseExp> "then" <then_:IfThenElseExp> => {
        let no_pos_exp = _Exp::IfExp {
            test,
            then_,
            else_: None,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },

    IfThenElseExp,
}

IfThenElseExp: Box<Exp> = {
    "if" <test:IfThenElseExp> "then" <then_:IfThenElseExp> "else" <else_:IfThenElseExp> => {
        let no_pos_exp = _Exp::IfExp {
            test,
            then_,
            else_: Some(else_),
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    BoolExp,
}

BoolExp: Box<Exp> = {
    <l: BoolExp> "|" <r: CompExp> => {
        let no_pos_exp = _Exp::IfExp {
            test: l,
            then_: posed_exp(_Exp::IntExp(1), 0, 0),
            else_: Some(r),
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    <l: BoolExp> "&" <r: CompExp> => {
        let no_pos_exp = _Exp::IfExp {
            test: l,
            then_: r,
            else_: Some(posed_exp(_Exp::IntExp(0), 0, 0)),
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    CompExp,
}

CompExp: Box<Exp> = {
    <l:AddExp> <o:CompOp> <r:AddExp> => {
        let no_pos_exp = _Exp::OpExp {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0);
    },
    AddExp,
}

CompOp: Oper = {
    "=" => Oper::EqOp,
    "<" => Oper::LtOp,
    ">" => Oper::GtOp,
    ">=" => Oper::GeOp,
    "<=" => Oper::LeOp,
    "<>" => Oper::NeqOp,
}

AddExp: Box<Exp> = {
    <l:AddExp> <o:AddOp> <r:MultExp> => {
        let no_pos_exp = _Exp::OpExp {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0);
    },
    MultExp,
}

AddOp: Oper = {
    "+" => Oper::PlusOp,
    "-" => Oper::MinusOp,
};

MultExp: Box<Exp> = {
    <l:MultExp> <o:MultOp> <r:Term> => {
        let no_pos_exp = _Exp::OpExp {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0);
    },
    Term,
};

MultOp: Oper = {
    "*" => Oper::TimesOp,
    "/" => Oper::DivideOp,
};

Term: Box<Exp> = {
    "(" ")" => {
        let no_pos_exp = _Exp::UnitExp;
        return posed_exp(no_pos_exp, 0, 0);
    },
    "nil" => {
        let no_pos_exp = _Exp::NilExp;
        return posed_exp(no_pos_exp, 0, 0);
    },
    "break" => {
        let no_pos_exp = _Exp::BreakExp;
        return posed_exp(no_pos_exp, 0, 0);
    },
    Num => {
        let no_pos_exp = _Exp::IntExp(<>);
        return posed_exp(no_pos_exp, 0, 0);
    },
    <s:Str> => {
        let no_pos_exp = _Exp::StringExp(s);
        return posed_exp(no_pos_exp, 0, 0);
    },
    VarEx => {
        let no_pos_exp = _Exp::VarExp(<>);
        return posed_exp(no_pos_exp, 0, 0);
    },
    <s:Sym> "(" ")" => {
        let no_pos_exp = _Exp::CallExp {
            func: s,
            args: vec![],
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    <s:Sym> "(" <f:CallFields> ")" => {
        let no_pos_exp = _Exp::CallExp {
            func: s,
            args: f,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    <typ: Sym> "{" "}" => {
        let no_pos_exp = _Exp::RecordExp {
            fields: vec![],
            typ,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    <typ: Sym> "{" <fields: RecFields> "}" => {
        let no_pos_exp = _Exp::RecordExp {
            fields,
            typ,
        };
        return posed_exp(no_pos_exp, 0, 0);
    },
    "(" <list: ExpList> ")" => {
        let no_pos_exp = _Exp::SeqExp(list);
        return posed_exp(no_pos_exp, 0, 0);
    },
    "(" <Expr> ")",
};

ExpList: Vec<Box<Exp>> = {
    <e: Expr> ";" <list: ExpMaybeList> => {
        let mut explist = vec![e];
        for ex in list {
            explist.push(ex);
        }

        return explist;
    },
}

ExpMaybeList: Vec<Box<Exp>> = {
    ExpList,
    Expr => {
        vec![<>]
    }
}

RecFields: Vec<(Symbol, Box<Exp>)> = {
    <f: RecField> "," <fields: RecFields> => {
        let mut flds = vec![f];

        for fld in fields {
            flds.push(fld);
        }

        return flds;
    },

    RecField => {
        vec![<>]
    },
}

RecField: (Symbol, Box<Exp>) = {
    <s: Sym> "=" <e: Expr> => {
        (s, e)
    },
}

CallFields: Vec<Box<Exp>> = {
    <e: Expr> "," <f: CallFields> => {
        let mut exps = vec![e];

        for ex in f {
            exps.push(ex);
        }

        return exps;
    },
    Expr => {
        vec![<>]
    }
}

VarEx: Var = {
    Sym => {
        return Var::SimpleVar(<>);
    },
    <v:VarEx> "[" <e:Expr> "]" => {
        let boxed_var = Box::new(v);
        return Var::SubscriptVar(boxed_var, e);
    },
    <v:VarEx> "." <s:Sym> => {
        let boxed_var = Box::new(v);
        return Var::FieldVar(boxed_var, s);
    },
}

Num: i64 = {
    <n:"Number"> => n
};

Sym: String = {
    <s:"Symbol"> => s
}

Str: String = {
    <s:"StringLiteral"> => s
}

DecsEx: Vec<Dec> = {
    <new: DecEx> <others: DecsEx> => {
        append_dec(new, others)
    },
    DecEx => {
        vec![<>]
    },
}

DecEx: Dec = {
    "type" <name:Sym> "=" <ty:TyEx> => {
        Dec::TypeDec(vec![_TypeDec::new(name, ty)])
    },
    VardecEx,
    FundecEx,
}

FundecEx: Dec = {
    "function" <name: Sym> "(" <params: TyFieldsEx> ")" "=" <body: Expr> => {
        Dec::FunctionDec(vec![_FunctionDec::new(name, params, None, body)])
    },
    "function" <name: Sym> "(" <params: TyFieldsEx> ")" ":" <result: Sym> "=" <body: Expr> => {
        Dec::FunctionDec(vec![_FunctionDec::new(name, params, Some(result), body)])
    },
}

VardecEx: Dec = {
    "var" <name: Sym> ":=" <init: Expr> => {
        Dec::VarDec(_VarDec::new(name, None, init))
    },
    "var" <name: Sym> ":" <typ: Sym> ":=" <init: Expr> => {
        Dec::VarDec(_VarDec::new(name, Some(typ), init))
    },
}

TyEx: Ty = {
    Sym => Ty::NameTy(<>),

    "{" <tyfields: TyFieldsEx> "}" => Ty::RecordTy(tyfields),
    "{" "}" => Ty::RecordTy(vec![]),

    "array" "of" <s: Sym> => Ty::ArrayTy(s),
}

TyFieldsEx: Vec<Field> = {
    <new:TyFieldEx> "," <fields:TyFieldsEx> => {
        let mut tys = vec![new];

        for field in fields {
            tys.push(field);
        }
        return tys;
    },
    TyFieldEx => {
        vec![<>]
    }
}

TyFieldEx: Field = {
    <name: Sym> ":" <ty_name: Sym> => {
        Field {
            name,
            escape: false,
            typ: Ty::NameTy(ty_name),
        }
    }
}

extern {
    type Location = usize;
    type Error = lexer::LexicalError;

    enum lexer::Tok {
        "." => lexer::Tok::Point,
        ":" => lexer::Tok::Colon,
        ":=" => lexer::Tok::Assign,
        "," => lexer::Tok::Comma,
        ";" => lexer::Tok::Semicolon,
        "(" => lexer::Tok::OpenParen,
        ")" => lexer::Tok::CloseParen,
        "[" => lexer::Tok::OpenBracket,
        "]" => lexer::Tok::CloseBracket,
        "{" => lexer::Tok::OpenBraces,
        "}" => lexer::Tok::CloseBraces,
        "&" => lexer::Tok::Ampersand,
        "|" => lexer::Tok::Pipe,
        "=" => lexer::Tok::Equals,
        "<" => lexer::Tok::Lt,
        "<=" => lexer::Tok::Lte,
        ">" => lexer::Tok::Gt,
        ">=" => lexer::Tok::Gte,
        "<>" => lexer::Tok::Neq,
        "+" => lexer::Tok::Plus,
        "-" => lexer::Tok::Minus,
        "*" => lexer::Tok::Times,
        "/" => lexer::Tok::Div,
        "type" => lexer::Tok::Type,
        "array" => lexer::Tok::Array,
        "of" => lexer::Tok::Of,
        "var" => lexer::Tok::Var,
        "function" => lexer::Tok::Function,
        "let" => lexer::Tok::Let,
        "in" => lexer::Tok::In,
        "end" => lexer::Tok::End,
        "if" => lexer::Tok::If,
        "then" => lexer::Tok::Then,
        "else" => lexer::Tok::Else,
        "while" => lexer::Tok::While,
        "do" => lexer::Tok::Do,
        "for" => lexer::Tok::For,
        "to" => lexer::Tok::To,
        "break" => lexer::Tok::Break,
        "nil" => lexer::Tok::Nil,
        "Symbol" => lexer::Tok::Symbol(<String>),
        "StringLiteral" => lexer::Tok::Str(<String>),
        "Number" => lexer::Tok::Number(<i64>),
        "/*" => lexer::Tok::OpenComen, // Shouldn't appear
        "*/" => lexer::Tok::CloseComen, // Shouldn't appear
        "//" => lexer::Tok::LineComen, // Shouldn't appear
        "Quote" => lexer::Tok::Quote, // Shouldn't appear
    }
}