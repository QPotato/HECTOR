use std::str::FromStr;
use crate::ast::tigerabs::*;

grammar;

pub Expr: Box<Exp> = {
    <l: Expr> "|" <r: CompExp> => {
        let no_pos_exp = _Exp::If {
            test: l,
            then_: posed_exp(_Exp::Int(1), 0, 0),
            else_: Some(r),
        };
        return posed_exp(no_pos_exp, 0, 0)
    },
    <l: Expr> "&" <r: CompExp> => {
        let no_pos_exp = _Exp::If {
            test: l,
            then_: r,
            else_: Some(posed_exp(_Exp::Int(0), 0, 0)),
        };
        return posed_exp(no_pos_exp, 0, 0)
    },
    CompExp,
}

pub CompExp: Box<Exp> = {
    <l:AddExp> <o:CompOp> <r:AddExp> => {
        let no_pos_exp = _Exp::Op {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0)
    },
    AddExp,
}

CompOp: Oper = {
    "=" => Oper::EqOp,
    "<" => Oper::LtOp,
    ">" => Oper::GtOp,
    ">=" => Oper::GeOp,
    "<=" => Oper::LeOp,
    "<>" => Oper::NeqOp,
}

AddExp: Box<Exp> = {
    <l:AddExp> <o:AddOp> <r:MultExp> => {
        let no_pos_exp = _Exp::Op {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0)
    },
    MultExp,
}

AddOp: Oper = {
    "+" => Oper::PlusOp,
    "-" => Oper::MinusOp,
};

MultExp: Box<Exp> = {
    <l:MultExp> <o:MultOp> <r:Term> => {
        let no_pos_exp = _Exp::Op {left: l, oper: o, right: r};
        return posed_exp(no_pos_exp, 0, 0)
    },
    Term,
};

MultOp: Oper = {
    "*" => Oper::TimesOp,
    "/" => Oper::DivideOp,
};

Term: Box<Exp> = {
    Num => {
        let no_pos_exp = _Exp::Int(<>);
        return posed_exp(no_pos_exp, 0, 0)
    },
    "(" <Expr> ")",
};

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};
